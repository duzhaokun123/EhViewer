apply plugin: 'com.android.application'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'
//apply plugin: 'kotlin-android'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"
    ndkVersion "21.3.6528147"
    signingConfigs {
        def keystorePwd = null
        def alias = null
        def pwd = null
        if (project.rootProject.file('local.properties').exists()) {
            Properties properties = new Properties()
            properties.load(project.rootProject.file('local.properties').newDataInputStream())
            keystorePwd = properties.getProperty("RELEASE_STORE_PASSWORD")
            alias = properties.getProperty("RELEASE_KEY_ALIAS")
            pwd = properties.getProperty("RELEASE_KEY_PASSWORD")
        }
        release {
            storeFile file("../ReleaseKey.jks")
            storePassword keystorePwd != null ? keystorePwd : System.getenv("KEYSTORE_PASS")
            keyAlias alias != null ? alias : System.getenv("ALIAS_NAME")
            keyPassword pwd != null ? pwd : System.getenv("ALIAS_PASS")
        }
    }
    defaultConfig {
        applicationId "com.duzhaokun123.ehviewer"
        minSdkVersion 29
        targetSdkVersion 30
        versionCode 128
        versionName "1.7.18.4"
        resConfigs "zh", "zh-rCN", "zh-rHK", "zh-rTW",
                "es", "ja", "ko", "fr", "de", "th"
        testOptions.unitTests.includeAndroidResources = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        disable 'MissingTranslation'
        abortOnError true
        checkReleaseBuilds true
    }

    buildTypes {
        release {
//            minifyEnabled true
//            shrinkResources true
            minifyEnabled false
            /* FIXME: do not minify, or get Error like:
                java.lang.VerifyError: Verifier rejected class d.t.c: android.animation.Animator d.t.c.q(android.view.ViewGroup, d.t.h0, d.t.h0) failed to verify: android.animation.Animator d.t.c.q(android.view.ViewGroup, d.t.h0, d.t.h0): [0x9D] copy1 v20<-v9 type=Reference: d.t.q0 cat=1 (declaration of 'd.t.c' appears in base.apk)
                    at d.t.b0.b(TransitionInflater.java:8)
                    at d.t.b0.b(TransitionInflater.java:62)
                    at d.t.b0.c(TransitionInflater.java:2)
                    at com.hippo.ehviewer.ui.scene.EnterGalleryDetailTransaction.onTransition(EnterGalleryDetailTransaction.java:5)
                    at com.hippo.scene.StageActivity.startScene(StageActivity.java:43)
                    at com.hippo.scene.StageActivity.startScene(StageActivity.java:1)
                    at com.hippo.scene.SceneFragment.startScene(SceneFragment.java:6)
                    at com.hippo.ehviewer.ui.scene.FavoritesScene.onItemClick(FavoritesScene.java:23)
                    at com.hippo.ehviewer.ui.scene.FavoritesScene$FavoritesAdapter.onItemClick(FavoritesScene.java:1)
                    ...
             */
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix ".debug"
//            signingConfig signingConfigs.release
        }
    }

    sourceSets {
        main {
            java.srcDirs += 'src/main/java-gen'
        }
    }

    splits.abi {
        enable true
        universalApk true
    }
}

tasks.withType(JavaCompile) {
    task -> task.dependsOn ":daogenerator:executeDaoGenerator"
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.biometric:biometric:1.0.1'
    implementation "androidx.browser:browser:1.3.0"
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation "androidx.fragment:fragment:1.2.5"
    implementation "androidx.preference:preference:1.1.1"
    implementation 'com.google.android.gms:play-services-oss-licenses:17.0.0'
    implementation 'com.google.android.material:material:1.3.0-alpha04'
    implementation 'com.getkeepsafe.taptargetview:taptargetview:1.13.0'
    implementation 'com.github.seven332.a7zip:extract-lite:1b21783' //TODO: 替换以减小包大小
    implementation 'com.github.seven332:android-resource:0.1.0'
    implementation 'com.github.seven332:beerbelly:0.1.4'
    implementation 'com.github.seven332:conaco:0.1.5-eh'
    implementation 'com.github.seven332:streampipe:0.1.0'
    implementation 'com.github.seven332:tuxiang:0.1.6'
    //noinspection GradleDependency
    implementation 'com.github.seven332:unifile:9ec57bcd8f'
    implementation 'com.github.seven332:yorozuya:0.1.2'
    implementation 'com.github.seven332:yorozuya-thread:0.1.1'
    implementation 'com.github.seven332:yorozuya-collect:0.1.4'
    implementation 'com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:1.0.0'
//    def appCenterSdkVersion = '3.3.1'
//    implementation "com.microsoft.appcenter:appcenter-analytics:${appCenterSdkVersion}"
//    implementation "com.microsoft.appcenter:appcenter-crashes:${appCenterSdkVersion}"
//    implementation "com.microsoft.appcenter:appcenter-distribute:${appCenterSdkVersion}"
    //noinspection GradleDependency
    implementation 'com.squareup.okhttp3:okhttp:3.14.9'
    implementation 'com.squareup.okhttp3:okhttp-dnsoverhttps:3.14.7'
    implementation 'com.takisoft.preferencex:preferencex:1.1.0'
    implementation 'com.github.NekoInverter:DrawerLayout:6ba992f8d4'
    implementation 'com.github.NekoInverter:Image:8fd6c1e1df'
    implementation 'com.github.NekoInverter.preferencex-android:preferencex-simplemenu:88f93154b2'
    implementation 'com.github.NekoInverter:RefreshLayout:7241e04739'
    implementation 'fadeapp.widgets:scrollless-recyclerView:1.0.2'
    implementation 'org.ccil.cowan.tagsoup:tagsoup:1.2.1'
    implementation 'org.greenrobot:greendao:2.2.1'
    implementation 'org.jsoup:jsoup:1.12.2'
    testImplementation 'junit:junit:4.13.1'
    testImplementation 'org.robolectric:robolectric:4.2.1'
    testImplementation 'org.jooq:joor:0.9.6'

    // GalleryView
    implementation project(':galleryview')

//    // Kotlin
//    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

configurations.all {
    resolutionStrategy {
        exclude group: 'com.github.seven332', module: 'okhttp'
        exclude group: 'com.github.seven332', module: 'image'
    }
}
